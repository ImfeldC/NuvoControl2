<?xml version="1.0" encoding="utf-8" ?>
<!-- 
XSLT  for generating build reports for CCNet Dashboard from unit test report generated by MSTest utility (V9.0.21022.8)
Author: Chamindu R. Munasinghe (cmunasinghe@gmail.com) 
Based on the original XSL by Roman V. Gavrilov (clavrg@hotmail.com)
-->
<xsl:transform version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
               xmlns:tt="http://microsoft.com/schemas/VisualStudio/TeamTest/2006">
  <xsl:output method="html"/>

  <xsl:template match="/">
    <!-- 
        TestOutcome:
            1 - Failed
						4 - Inconclusive
            10 - Passed
        -->
    <xsl:variable name="pass_count" select="cruisecontrol/build/tt:TestRun/tt:ResultSummary/tt:Counters/@passed"/>

    <xsl:variable name="inconclusive_count" select="cruisecontrol/build/tt:TestRun/tt:ResultSummary/tt:Counters/@inconclusive"/>
    <xsl:variable name="failed_count" select="cruisecontrol/build/tt:TestRun/tt:ResultSummary/tt:Counters/@failed"/>
    <xsl:variable name="total_count" select="cruisecontrol/build/tt:TestRun/tt:ResultSummary/tt:Counters/@total"/>

    <table class="section-table" width="100%">
      <tr>
        <td class="sectionheader">
          Tests run: <xsl:value-of select="$total_count"/>, Failures: <xsl:value-of select="$failed_count"/>, Inconclusive: <xsl:value-of select="$inconclusive_count"/>
        </td>
      </tr>

      <tr>
        <td>
          <table>
            <tr>
              <td width="100">
                <b>Passed:</b>
              </td>
              <td>
                <xsl:value-of select="$pass_count"/>
              </td>
            </tr>
            <tr>
              <td>
                <b>Failed:</b>
              </td>
              <td>
                <xsl:value-of select="$failed_count"/>
              </td>
            </tr>
            <tr>
              <td>
                <b>Inconclusive:</b>
              </td>
              <td>
                <xsl:value-of select="$inconclusive_count"/>
              </td>
            </tr>
          </table>
        </td>
      </tr>
      <!-- BAR -->
      <tr>
        <td width="100%">
          <table border="1" width="100%">
            <tr height="20">
              <xsl:choose>
                <xsl:when test="$total_count=$pass_count">
                  <td bgcolor="00FF33" align="center">
                    <b>PASSED</b>
                  </td>
                </xsl:when>
                <xsl:otherwise>
                  <td bgcolor="FF0000" align="center">
                    <b>FAILED</b>
                  </td>
                </xsl:otherwise>
              </xsl:choose>
            </tr>
          </table>
        </td>
      </tr>
      <!-- DETAILS TABLE -->
      <tr>
        <td>
          <table border="1" width="100%" frame="box">
            <tr bgcolor="#2288FF">
              <th align="center" width="40">Test Name</th>
              <th align="left">Outcome</th>
              <th align="left">Duration</th>
              <th align="left">Message</th>
              <th align="left">Stack Trace</th>
            </tr>

            <xsl:for-each select="cruisecontrol/build/tt:TestRun/tt:Results/tt:UnitTestResult">
              <xsl:variable name="BgColor">
                <xsl:choose>
                  <xsl:when test="@outcome = 'Passed'">#00FF33</xsl:when>
                  <xsl:when test="@outcome = 'Failed'">red</xsl:when>
                  <xsl:when test="@outcome = 'Inconclusive'">#FFCC00</xsl:when>
                  <xsl:otherwise>#3399FF</xsl:otherwise>
                </xsl:choose>
              </xsl:variable>
              
              <tr>
                <xsl:attribute name="bgcolor">
                  <xsl:value-of select="$BgColor"/>
                </xsl:attribute>
                <td>
                  <xsl:value-of select="@testName"/>
                </td>
                <td>
                  <xsl:value-of select="@outcome"/>
                </td>
                <td>
                  <xsl:value-of select="@duration"/>
                </td>
                <td>
                    <xsl:value-of select="tt:Output/tt:ErrorInfo/tt:Message"/>
                </td>
                <td>
                  <xsl:value-of select="tt:Output/tt:ErrorInfo/tt:StackTrace"/>
                </td>                
              </tr>
            </xsl:for-each>
          </table>
        </td>
      </tr>
    </table>
  </xsl:template>

</xsl:transform>
